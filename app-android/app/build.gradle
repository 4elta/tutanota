apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "de.tutao.tutanota"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 348050
        versionName "3.48.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [contentProviderAuthority: 'de.tutao.fileprovider.debug']
        }
        staging {
            minifyEnabled true
            initWith release
            manifestPlaceholders = [contentProviderAuthority: 'de.tutao.fileprovider.test']
            applicationIdSuffix ".test"
        }
        release {
            minifyEnabled true
            manifestPlaceholders = [contentProviderAuthority: 'de.tutao.fileprovider']
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "websource"

    productFlavors {
        localhost {
            dimension "websource"
            buildConfigField 'String', 'RES_ADDRESS', "\"http://" + InetAddress.getLocalHost().getHostName().split("\\.")[0] + ":9000/client/build/app\""
        }
        local {
            dimension "websource"
            buildConfigField 'String', 'RES_ADDRESS', "\"file:///android_asset/tutanota/app.html\""
        }
        localDist {
            dimension "websource"
            buildConfigField 'String', 'RES_ADDRESS', "\"file:///android_asset/tutanota/dist/app.html\""
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = output.outputFileName.replace("app-release.apk", "tutanota-${variant.versionName}.apk")
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"' + it.manifestPlaceholders['contentProviderAuthority'] + '"'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.jdeferred:jdeferred-core:1.2.4'
    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support:support-core-utils:28.0.0'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.3.2"
    androidTestImplementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1'
}
repositories {
    mavenCentral()
}
